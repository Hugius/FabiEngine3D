4 42
META script_type_update

/// Temporary values
DEC cursorX = fe3d:cursor_get_position_x()
DEC cursorY = fe3d:cursor_get_position_y()

/// Dynamic background
DEC backgroundX = cursorX
DEC backgroundY = cursorY
MUL backgroundX -0.25
MUL backgroundY -0.25
fe3d:image_set_position("menu_background", backgroundX, backgroundY)

/// Handle buttons hover
STR buttonID = ""
DEC buttonWidth = 0.0
DEC buttonHeight = 0.0
DEC textWidth = 0.0
DEC textHeight = 0.0
DEC buttonX = 0.0
DEC buttonY = 0.0
DEC xDifference = 0.0
DEC yDifference = 0.0
INT index = 0
INT listSize = misc:get_list_size("_menuButtonIDs")
BOOL hovered = <false>
LOOP:
    IF index IS listSize:
        BREAK
    ELSE:
        EDIT buttonID = _menuButtonIDs[index]
        EDIT buttonWidth = fe3d:image_get_width(buttonID)
        EDIT buttonHeight = fe3d:image_get_height(buttonID)
        EDIT textWidth = fe3d:text_get_width(buttonID)
        EDIT textHeight = fe3d:text_get_height(buttonID)
        MUL buttonWidth 0.5
        MUL buttonHeight 0.5
        EDIT buttonX = fe3d:image_get_position_x(buttonID)
        EDIT buttonY = fe3d:image_get_position_y(buttonID)
        EDIT xDifference = buttonX
        DECR xDifference cursorX
        EDIT xDifference = math:abs(xDifference)
        EDIT yDifference = buttonY
        DECR yDifference cursorY
        EDIT yDifference = math:abs(yDifference)
        EDIT hovered = (xDifference LESS buttonWidth AND yDifference LESS buttonHeight)
        MUL buttonWidth 2.0
        MUL buttonHeight 2.0
        IF hovered IS <true>:
            IF _hoveredMenuButtonID IS "":
                MUL buttonWidth 1.25
                MUL buttonHeight 1.25
                MUL textWidth 1.25
                MUL textHeight 1.25
                fe3d:image_set_size(buttonID, buttonWidth, buttonHeight)
                fe3d:text_set_size(buttonID, textWidth, textHeight)
                EDIT _hoveredMenuButtonID = buttonID
                BREAK
        ELIF _hoveredMenuButtonID IS buttonID:
            DIV buttonWidth 1.25
            DIV buttonHeight 1.25
            DIV textWidth 1.25
            DIV textHeight 1.25
            fe3d:image_set_size(buttonID, buttonWidth, buttonHeight)
            fe3d:text_set_size(buttonID, textWidth, textHeight)
            EDIT _hoveredMenuButtonID = ""
        INCR index 1
